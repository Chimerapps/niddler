group 'com.icapps'

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 25
    buildToolsVersion "24.0.3"

    defaultConfig {
        consumerProguardFiles 'proguard-rules.txt'
    }
}

dependencies {
    compile 'org.java-websocket:Java-WebSocket:1.3.0'
    provided 'com.squareup.okhttp3:okhttp:3.4.2'

    compile "com.android.support:support-compat:25.0.1"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.getSrcDirs()
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.getSrcDirs()
}

publishing {
    repositories {
        maven {
            url "s3://icappsmaven/release"
            credentials(AwsCredentials) {
                accessKey rootProject.ext.awsKey
                secretKey rootProject.ext.awsSecretKey
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId "$ARTIFACT_PACKAGE"
            version "$ARTIFACT_VERSION"
            artifactId "$ARTIFACT_NAME"
            artifact("$buildDir/outputs/aar/niddler-lib-release.aar")
            artifact sourceJar {
                classifier "sources"
            }
            artifact androidJavadocsJar

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    if (it instanceof ExternalModuleDependency) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}
